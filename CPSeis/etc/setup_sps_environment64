#! /bin/csh -f
#<CPS_v1 type="PROGRAM" pretag="#"/>


#<copyright>
#*******************************************************************************
#***********                    COPYRIGHT NOTICE                     ***********
#*********** CONFIDENTIAL AND PROPRIETARY INFORMATION OF CONOCO INC. ***********
#***********  PROTECTED BY THE COPYRIGHT LAW AS AN UNPUBLISHED WORK  ***********
#*******************************************************************************
#</copyright>


#<brief_doc>
#-------------------------------------------------------------------------------
#                         C P S   D O C U M E N T A T I O N
#
# Name       : setup_sps_environment
# Category   : stand-alone
# Written    : 2003-03-18   by: Tom Stoeckley
# Revised    : 2007-04-10   by: Dave Glover
# Maturity   : scripts
# Purpose    : Setup ~sps environment from the .cshrc file.
# Portability: No known limitations.
#
#-------------------------------------------------------------------------------
#</brief_doc>


#<descript_doc>
#-------------------------------------------------------------------------------
#                          GENERAL DESCRIPTION
#
# This script should be called from individual user .cshrc files to
# set up the appropriate environment variables and aliases which will
# provide access to compilers and allow easy development work for ~sps
# and ~spws programmers, developers, and users.
#
# To call this script, simply put the following into your .cshrc file:
#
#                     source setup_sps_environment
#
# If this script is not reachable through the PATH variable (before it is
# added to by this script), you must specify the complete path, which can be
# provided to you by the ~sps system administrator.  Currently this will work:
#
#       source /usr/app/vendors/sps/scripts/setup_sps_environment
#
# If you need to set some environment variables, paths, or aliases
# differently, you can simply redefine them in your .cshrc file after
# sourcing setup_sps_environment.
#
#-------------------------------------------------------------------------------
#</descript_doc>


#<history_doc>
#-------------------------------------------------------------------------------
#                           REVISION HISTORY
#
#     Date        Author     Description
#     ----        ------     -----------
# 18. 2007-04-10  Goodger    In testing whether SPS_ENVIRONMENT variable exists, 
#                            remove the == 1 portion of the if statment.  This
#                            syntax does not work in rhel 4.  Kruger Corn.
# 17. 2007-02-27  Glover     Do not set LD_ASSUME_KERNEL for nodes in cps_no_assume.dat list
# 16. 2007-02-02  Goodger    Add an spws path without the architecture directory.
# 15. 2006-08-22  Goodger    Added another path to JAVA_DIR.
# 14. 2006-08-14  Goodger    Added env var JAVA_DIR and included LD_LIBRARY_PATH.
# 13. 2005-10-28  Goodger    Change USAGE_HOST to cop_usage from cps_usage.
# 12. 2005-10-25  Goodger    Set LD_ASSUME_KERNEL to 2.4.1.  This gets rid
#                            if incorrect build binary message on worker
#                            nodes.
# 11  2005-08-29  Goodger    Add env variable USAGE_HOST.
# 10. 2004-10-21  Wardrop    Change XFILESEARCHPATH to include default system
#                            on Linux systems.
#  9. 2004-06-07  Goodger    Remove /apps/ow from LD_LIBRARY_PATH. This
#                            should be needed only for landmark jobs and it
#                            is requiring that the file system be mounted
#                            for all jobs.
#  8. 2004-02-16  Stoeckley  Use /apps/ow for all architectures (remove
#                             /appl/ow for solaris).
#  7. 2003-11-06  Goodger    Remove manpath command and replace with a
#                            specified path.
#  6. 2003-10-22  Goodger    Change SPWSINSTALLDIR from /appl/SPWS to
#                             /usr/app/vendors/spws
#  5. 2003-10-10  Stoeckley  Change to conditionally return just before adding
#                             to path variables instead of right away; add
#                             access to the Portland Group compiler and to
#                             Landmark libraries; fix MANPATH for linux; set
#                             OWHOME, add to LD_LIBRARY_PATH.
#  4. 2003-06-16  Goodger    Remove echos to screen.
#  3. 2003-06-16  Stoeckley  Add SGI64 and ALTIX architectures; provide access
#                             to intel compilers; move solaris compiler setups
#                             to new scripts; add gmake alias for SGI; add
#                             SPWSMAKEDIR; add HOST if missing; add additions
#                             to MANPATH and XFILESEARCHPATH; add printout.
#  2. 2003-04-04  Stoeckley  Add condition to exit immediately if previously
#                             called.
#  1. 2003-03-18  Stoeckley  Initial version.
#
#-------------------------------------------------------------------------------
#</history_doc>


##------------------------- set ARCHITECTURE -------------------------------##
##------------------------- set ARCHITECTURE -------------------------------##
##------------------------- set ARCHITECTURE -------------------------------##



if(`uname` == SunOS) then
                                    setenv ARCHITECTURE sparc-sun-solaris
else if(`uname` == Linux) then
                                    setenv ARCHITECTURE ix86-intel-linux
else
         echo ++++ setup_sps_environment unsupported architecture:
         echo ++++ `uname -a`
         exit
endif


##------------------- set general environment variables --------------------##
##------------------- set general environment variables --------------------##
##------------------- set general environment variables --------------------##

if($?CPSEIS_INSTALL_DIR == 0 || $?CPSEIS_ARCH == 0 ) then
	echo "Must define cpseis installation directory.  ex: /home/your_user_name/cpseis"
    echo "Add this to your .cshrc: 'setenv CPSEIS_INSTALL_DIR /your_installation_directory/cpseis' "
	echo "If using the INTEL compiler versions, be sure to add this:"
	echo "setenv INTEL intel32 <--- if using the 32 bit version, or"
	echo "setenv INTEL intel64 <--- if using the 64 bit version."
	exit 1
endif
setenv SPSDIR             ${CPSEIS_INSTALL_DIR}

# create custom cps_config.dat for CPSEIS_INSTALL_DIR
cat $SPSDIR/etc/cpseis64_config.dat | sed "s:INSTALLDIR:${CPSEIS_INSTALL_DIR}:g" >$SPSDIR/etc/cps_config.dat
# --- ---- -------------------------
#echo "SPSDIR = $SPSDIR"
#cat $SPSDIR/etc/cps_config.dat
# --- for debugging ((( above))) ---
setenv SPWSDIR            $SPSDIR
setenv SPSINSTALLDIR      $SPSDIR
setenv SPWSINSTALLDIR     $SPSDIR
setenv XAPPLRESDIR        $HOME/app-defaults
setenv SPWSMAKEDIR        $SPSDIR/etc
setenv CFECUSTOMDIR       $SPSDIR/etc
setenv CFECUSTOMSCRIPTS   $SPSINSTALLDIR
setenv CFECUSTOMPROGRAMS  $SPSINSTALLDIR/bin/$ARCHITECTURE
setenv CPS_CONFIG_FILE    $SPSDIR/etc/cps_config.dat
#you must have this next line for cnfg_crou.c.  Without it, you always look at the default values.
setenv cps_config_file	  $SPSDIR/etc/cps_config.dat 
setenv USAGE_HOST         $HOSTNAME
setenv JAVA_HOME		  /apps/geophysics
setenv JAVA_DIR_ROOT      $JAVA_HOME/jdk/jre/lib
setenv JAVA_DIR          $JAVA_DIR_ROOT/i386:$JAVA_DIR_ROOT/i386/client

#echo $SPSDIR $SPWSDIR $SPSINSTALLDIR $SPWSINSTALLDIR $XAPPLRESDIR $SPWSMAKEDIR $CFECUSTOMDIR $CFECUSTOMSCRIPTS $CFECUSTOMPROGRAMS $CPS_CONFIG_FILE $USAGE_HOST $JAVA_HOME $JAVA_DIR_ROOT $JAVA_DIR

#echo "-------------------------------- READING NEW CONFIG FILE ---------------------"
#echo "cps_config_file="$cps_config_file
#echo "CPS_CONFIG_FILE="$CPS_CONFIG_FILE

if ($?HOST == 0) then
  setenv HOST `uname -n`
endif

##----------do not set LD_ASSUME_KERNEL on Fedora Core 6 ld nodes-----------##
## filereserve will fail on hoepld11-hoepld23 if LD_ASSUME_KERNEL is set

if (-e $SPSINSTALLDIR/etc/cps_no_assume.dat) then
  if (`grep -v \# $SPSINSTALLDIR/etc/cps_no_assume.dat |& grep $HOST | wc -l` == 0) then
     setenv LD_ASSUME_KERNEL   2.4.1
  else
     unsetenv LD_ASSUME_KERNEL
  endif
else
  setenv LD_ASSUME_KERNEL   2.4.1
endif

##----------------- reset if different for solaris -------------------------##

setenv OWHOME /apps/ow

##----------------- return if this script was already called ---------------##
##----------------- return if this script was already called ---------------##
##----------------- return if this script was already called ---------------##

######if($?SPS_ENVIRONMENT == 1) then
#echo "Testing for SPS_ENVIRONMENT"
if($?SPS_ENVIRONMENT == 1) then
   #echo "SPS ENVIRONMENT already set up, doing it again."
	unset SPS_ENVIRONMENT
#else
	#echo "SPS ENVIRONMENT not set up."
endif
#echo "Tested for SPS_ENVIRONMENT"


setenv SPS_ENVIRONMENT true


##------------------ add to manpath environment variable -------------------##
##------------------ add to manpath environment variable -------------------##
##------------------ add to manpath environment variable -------------------##


if($?MANPATH == 0 && $ARCHITECTURE == ix86-intel-linux) then
   setenv ARCHITECTURE linux32
   setenv MANPATH /usr/share/man:/usr/local/man:/usr/X11R6/man
   setenv MANPATH "$MANPATH":/usr/applinux/man:/usr/kerberos/man:/usr/man
endif

if($?MANPATH == 0 && $ARCHITECTURE == sparc-sun-solaris) then
   setenv MANPATH /usr/man:/usr/share/man:/usr/local/man:/admin/man
endif

if($?MANPATH == 0) then
   setenv MANPATH $SPSDIR/man
else
   setenv MANPATH $SPSDIR/man:$MANPATH
endif

##------------------ add to path environment variable ---------------------##
##------------------ add to path environment variable ---------------------##
##------------------ add to path environment variable ---------------------##

#echo "PATH=$PATH"
#echo "SPSINSTALLDIR=$SPSINSTALLDIR"

setenv PATH $SPSINSTALLDIR/scripts:$PATH
setenv PATH $SPWSINSTALLDIR/scripts:$PATH
setenv PATH $SPSINSTALLDIR/bin/${ARCHITECTURE}:$PATH
setenv PATH $SPWSINSTALLDIR/bin:$PATH 
setenv PATH $SPWSINSTALLDIR/bin/${ARCHITECTURE}:$PATH


##------------- add to xfilesearchpath environment variable ----------------##
##------------- add to xfilesearchpath environment variable ----------------##
##------------- add to xfilesearchpath environment variable ----------------##


if ($?XFILESEARCHPATH == 0) then
  if ($ARCHITECTURE == ix86-intel-linux) then
    setenv XFILESEARCHPATH /usr/lib/X11/%T/%N%S:$SPWSINSTALLDIR/app-defaults/$ARCHITECTURE/%N
  else
    setenv XFILESEARCHPATH $SPWSINSTALLDIR/app-defaults/$ARCHITECTURE/%N
  endif
else
  setenv XFILESEARCHPATH \
             $SPWSINSTALLDIR/app-defaults/$ARCHITECTURE/%N:$XFILESEARCHPATH
endif


##------------- add to ld_library_path environment variable ----------------##
##------------- add to ld_library_path environment variable ----------------##
##------------- add to ld_library_path environment variable ----------------##

if($?LD_LIBRARY_PATH == 0) then
  setenv LD_LIBRARY_PATH $SPSINSTALLDIR/lib/${ARCHITECTURE}:${JAVA_DIR}
else
  setenv LD_LIBRARY_PATH $SPSINSTALLDIR/lib/${ARCHITECTURE}:${JAVA_DIR}:${LD_LIBRARY_PATH}
endif
## INTEL set to intel32 or intel64 if you linked with either of those compilers.
if($?INTEL != 0) then
  setenv LD_LIBRARY_PATH ${LD_LIBRARY_PATH}:${CPSEIS_INSTALL_DIR}/lib/${INTEL}
endif


exit

#-------------------------------- end -------------------------------------##
#-------------------------------- end -------------------------------------##
#-------------------------------- end -------------------------------------##

