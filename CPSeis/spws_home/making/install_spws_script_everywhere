#! /bin/csh -f
#<CPS_v1 type="SCRIPT" pretag="#"/>

#<copyright>
#*******************************************************************************
#***********                    COPYRIGHT NOTICE                     ***********
#*********** CONFIDENTIAL AND PROPRIETARY INFORMATION OF CONOCO INC. ***********
#***********  PROTECTED BY THE COPYRIGHT LAW AS AN UNPUBLISHED WORK  ***********
#*******************************************************************************
#</copyright>


#<brief_doc>
#-------------------------------------------------------------------------------
#                         C P S   S C R I P T
#
# Name       : install_spws_script_everywhere
# Category   : stand-alone
# Written    : 2004-05-24   by: Tom Stoeckley
# Revised    : 2004-05-24   by: Tom Stoeckley
# Maturity   : beta
# Purpose    : Shell script to install an SPWS script for public use.
# Portability: No known limitations.
#
#-------------------------------------------------------------------------------
#</brief_doc>


#<descript_doc>
#-------------------------------------------------------------------------------
#                             DOCUMENTATION
#
# This script is a utility to install an SPWS script for public use.
#
# This script performs the following task:
#
#   (1) Tags the latest version of the script source code with the same
#       date used by install_spws_script to identify the installed script.
#
#   (2) Runs install_spws_script on an appropriate node for each desired
#       architecture.
#
# For example:  If the tag is 21May2004, the installed script will be
#               identified with extension 21May2004.
#
# You must type the following:
#
#                 install_spws_script_everywhere <script>
#
# where <script> is the name of the script to install.
#
# The full path must be specified if <script> is not in the local directory.
#
# Task (1) is performed on the local node where this script is run.
# This node must support RCS.
#
# Task (2) is performed by rsh to each node listed in the
# installation_locations file.
#
# Currently, the following architectures are supported, although this can
# be changed by editing the installation_locations file:
#
#           node           architecture
#           ----           ------------
#           hoeplc03       ix86-intel-linux
#           hotce03        sparc-sun-solaris
#
#-------------------------------------------------------------------------------
#                            INPUT REQUIRMENTS
#
# The following environment variables must be set:
#
#         SPWSDIR          normally ~spws
#         SPWSMAKEDIR      normally ~spws/making
#         SPWSINSTALLDIR   normally ~spws/install or /usr/app/vendors/spws
#         ARCHITECTURE     normally sparc-sun-solaris or ix86-intel-linux
#         USER             must be spws
#
# Note: ~spws/install is simply a link to /usr/app/vendors/spws.
#
# The following file must exist:
#
#                $SPWSMAKEDIR/installation_locations
#
#-------------------------------------------------------------------------------
#</descript_doc>


#<history_doc>
#-------------------------------------------------------------------------------
#                           REVISION HISTORY
#
#     Date        Author     Description
#     ----        ------     -----------
#  1. 2004-05-24  Stoeckley  Initial version.
#
#-------------------------------------------------------------------------------
#</history_doc>


#-------------------------------------------------------------------------------
# RCS Id : $Id: install_spws_script_everywhere,v 1.1 2004/05/25 21:47:46 spws Exp spws $
#-------------------------------------------------------------------------------


##-------------------- verify environment variables ------------------------##
##-------------------- verify environment variables ------------------------##
##-------------------- verify environment variables ------------------------##


if($?SPWSDIR == 0 || $SPWSDIR == "") then
     echo environment variable SPWSDIR not set.
     exit
endif

if($?SPWSMAKEDIR == 0 || $SPWSMAKEDIR == "") then
     echo environment variable SPWSMAKEDIR not set.
     exit
endif

if($?SPWSINSTALLDIR == 0 || $SPWSINSTALLDIR == "") then
     echo environment variable SPWSINSTALLDIR not set.
     exit
endif

if($?ARCHITECTURE == 0 || $ARCHITECTURE == "") then
     echo environment variable ARCHITECTURE not set.
     exit
endif


if($?USER == 0 || $USER != "spws") then
     echo environment variable USER must be spws.
     exit
endif


##----------------------- verify input file -------------------------------##
##----------------------- verify input file -------------------------------##
##----------------------- verify input file -------------------------------##


if(! -f $SPWSMAKEDIR/installation_locations) then
     echo missing installation_locations file.
     exit
endif


##------------------------- get input variables ---------------------------##
##------------------------- get input variables ---------------------------##
##------------------------- get input variables ---------------------------##


set script = $1

if($script == "") then
  echo "missing script name"
  exit 1
endif


##-------------------- define internal variables ---------------------------##
##-------------------- define internal variables ---------------------------##
##-------------------- define internal variables ---------------------------##


set platnodes = `cat $SPWSMAKEDIR/installation_locations`

set tag = "`date +%d``date +%b``date +%Y`"

if($script == $script:t) then
  set script    = `pwd`/$script
  set directory = `pwd`
else
  set directory = $script:h
endif


##------------------------- tag the RCS file ------------------------------##
##------------------------- tag the RCS file ------------------------------##
##------------------------- tag the RCS file ------------------------------##


echo ""

if(-d $directory && -d $directory/RCS) then
   echo tagging RCS file in $directory
   rcs -n${tag}: $directory/RCS/$script:t,v
endif


##------------------------- install the script ----------------------------##
##------------------------- install the script ----------------------------##
##------------------------- install the script ----------------------------##


foreach platnode ($platnodes)
   echo "$platnode" | grep '#' > /dev/null && continue
   if($platnode == "") continue

   set node     = $platnode:r
   set command  = "install_spws_script $script"

   echo ""
   echo "using node $node to install script $script:t"
   echo ""

   rsh $node $command
end

echo ""


##-------------------------------- end -------------------------------------##
##-------------------------------- end -------------------------------------##
##-------------------------------- end -------------------------------------##

