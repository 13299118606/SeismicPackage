#! /bin/csh -f
#<CPS_v1 type="SCRIPT" pretag="#"/>


#<copyright>
#*******************************************************************************
#***********                    COPYRIGHT NOTICE                     ***********
#*********** CONFIDENTIAL AND PROPRIETARY INFORMATION OF CONOCO INC. ***********
#***********  PROTECTED BY THE COPYRIGHT LAW AS AN UNPUBLISHED WORK  ***********
#*******************************************************************************
#</copyright>


#<brief_doc>
#-------------------------------------------------------------------------------
#                         C P S   S C R I P T
#
#   Name   : compare
#   Written: 1999-12-21   by: Tom Stoeckley
#   Revised: 2004-06-02   by: Tom Stoeckley
#   Purpose: Compare local files with system files to look for differences.
#
#------------------------------------------------------------------------------
#</brief_doc>


#<descript_doc>
#------------------------------------------------------------------------------
#                            DOCUMENTATION
#
# This script compares local files with those in ~spws and ~sps system
# directories and lists the results.  The intention is to make it easy for a
# developer to see which of his files need to be updated in system directories,
# or vice versa.
#
# This script can be used in a developer's directory to compare local files
# with those in ~spws and ~sps.  This script can also be used in an ~spws
# directory to compare local files with those in ~sps and /usr/app/vendors/spws.
#
# To run this script, simply type the word compare on the command line,
# optionally followed by one of these code words:
#
#   code "all"      prints a line for each file.
#   code "matching" prints a line for each matching file only.
#   code "missing"  prints a line for each missing file only.
#   code "diff"     prints a line for each non-matching file only.
#   code "mgdiff"   prints a line for each non-matching file only.
#   code "xdiff"    prints a line for each non-matching file only.
#   code "default"  prints a line for each file except for matching files.
#   code "reverse"  prints a line for each non-matching file in reverse order.
#   code "multiple" prints a line for each file found more than once.
#   code "help"     prints help.
#
# The user can then do a diff or mgdiff or xdiff to see the details of files
# which differ.  This can be done simply by triple-clicking on a listed line
# and then button-2-clicking in another xterm.
#
# If a second command line argument is specified, it should be a directory
# to which the local files are compared, instead of the system directories
# listed in DETAILS below.
#
# This script makes the following temporary files which it then deletes:
#   aa_tempfile  (list of source files in ~sps, ~spws, and local directory)
#   bb_tempfile  (list of source files in ~sps, ~spws, and local directory)
#     (these file alternate as some entries are deleted with grep -v)
#
#-------------------------------------------------------------------------------
#                          INPUT REQUIRMENTS
#
# The following environment variables must be set:
#
#       ARCHITECTURE        (compute node architecture)
#       SPWSDIR             (location of the ~spws home directory)
#       SPWSMAKEDIR         (location of some ~spws programs and scripts)
#       SPWSINSTALLDIR      (location of the ~spws install directory)
#       SPSDIR              (location of the ~sps home directory)
#       SPSINSTALLDIR       (location of the ~sps install directory)
#
# This script needs the following executable:
#
#       ~stoectr/making/$ARCHITECTURE/sps_comparisons
#
#------------------------------------------------------------------------------
#                              DETAILS
#
# The system directories which are searched depend on the name of the
# local directory in which this script is executed:
#
# For any current working directory:
#
#      $SPSDIR/templates/               (all files)
#
#      $SPSDIR/alpha/include/           (extensions .h and .hh)
#      $SPSDIR/alpha/primitives/*/      (extensions .f90 .c and .cc)
#      $SPSDIR/alpha/processes/*/       (extensions .f90 .c and .cc)
#
#      $SPSDIR/beta/include/            (extensions .h and .hh)
#      $SPSDIR/beta/primitives/*/       (extensions .f90 .c and .cc)
#      $SPSDIR/beta/processes/*/        (extensions .f90 .c and .cc)
#      $SPSDIR/beta/programs/*/         (extensions .f90 .c and .cc)
#      $SPSDIR/beta/scripts/            (all files)
#
#      $SPSDIR/production/include/      (extensions .h and .hh)
#      $SPSDIR/production/primitives/*/ (extensions .f90 .c and .cc)
#      $SPSDIR/production/processes/*/  (extensions .f90 .c and .cc)
#      $SPSDIR/production/programs/*/   (extensions .f90 .c and .cc)
#      $SPSDIR/production/scripts/      (all files)
#
# For current working directory "making" or "custom" or "documentation":
#
#      $SPWSINSTALLDIR/scripts/         (all files)
#  <>  $SPWSDIR/making/                 (all files)
#      $SPSDIR/etc/                     (all files)
#      $SPSINSTALLDIR/scripts/          (all files)
#
# For current working directory "app-defaults":
#
#  <>  $SPWSDIR/app-defaults/           (all files)
#      $SPWSINSTALLDIR/app-defaults/    (all files)
#
# For any current working directory EXCEPT "making" or "app-defaults"
# or "cps" or "archives" or "documentation":
#
#  <>  $SPWSDIR/include/                (extensions .h .hh)
#  <>  $SPWSDIR/include/*/              (extensions .h .hh)
#  <>  $SPWSDIR/util/*/                 (extensions .f .f90 .c .h)
#  <>  $SPWSDIR/oop/*/                  (extensions .f .f90 .c .h .cc .hh)
#  <>  $SPWSDIR/programs/*/             (extensions .f .f90 .c .h .cc .hh)
#
# File names containing test_ in the name, and a few other files with special
# names, are always omitted from the list of local files which are compared
# with system files.
#
# If the USER name is spws, the system directories marked above with <> are
# not searched.
#
#------------------------------------------------------------------------------
#</descript_doc>


#<history_doc>
#------------------------------------------------------------------------------
#                           REVISION HISTORY
#
#       Date      Author     Description
#       ----      ------     -----------
# 3.   2004-06-02 Stoeckley  Fixup to work for both a developer and ~spws.
# 2.   2003-11-04 Stoeckley  Various changes.
# 1.   1999-12-20 Stoeckley  First Version.
#
#------------------------------------------------------------------------------
#</history_doc>


##--------------------- verify environment variables ------------------------##
##--------------------- verify environment variables ------------------------##
##--------------------- verify environment variables ------------------------##


if($?ARCHITECTURE == 0 || $ARCHITECTURE == "") then
     echo environment variable ARCHITECTURE not set
     exit
endif

if($?SPWSDIR == 0 || $SPWSDIR == "") then
     echo environment variable SPWSDIR not set
     exit
endif

if($?SPWSMAKEDIR == 0 || $SPWSMAKEDIR == "") then
     echo environment variable SPWSMAKEDIR not set
     exit
endif

if($?SPWSINSTALLDIR == 0 || $SPWSINSTALLDIR == "") then
     echo environment variable SPWSINSTALLDIR not set
     exit
endif

if($?SPSDIR == 0 || $SPSDIR == "") then
     echo environment variable SPSDIR not set
     exit
endif

if($?SPSINSTALLDIR == 0 || $SPSINSTALLDIR == "") then
     echo environment variable SPSINSTALLDIR not set
     exit
endif


##------------------------ print variables -------------------------------##
##------------------------ print variables -------------------------------##
##------------------------ print variables -------------------------------##


echo " "
echo ARCHITECTURE ======= $ARCHITECTURE
echo SPWSDIR ============ $SPWSDIR
echo SPWSMAKEDIR ======== $SPWSMAKEDIR
echo SPWSINSTALLDIR ===== $SPWSINSTALLDIR
echo SPSDIR ============= $SPSDIR
echo SPSINSTALLDIR ====== $SPSINSTALLDIR
echo " "


##---------------------- save command line argument -------------------------##
##---------------------- save command line argument -------------------------##
##---------------------- save command line argument -------------------------##


if ($1 == "") then
     set code = "default"
else
     set code = $1
endif

echo $code  >  aa_tempfile


##----------------- save list of files in specified directory ---------------##
##----------------- save list of files in specified directory ---------------##
##----------------- save list of files in specified directory ---------------##


if ($2 == `pwd`) then

  echo "--> trying to compare with same directory"
  exit

else if ($2 != "") then

  echo "--> getting list of source files in $2  ..."
  ls $2/*                  >> aa_tempfile

endif


##---------------- save list of system scripts and makefiles -----------------##
##---------------- save list of system scripts and makefiles -----------------##
##---------------- save list of system scripts and makefiles -----------------##


if ($2 == "") then
if ($cwd:t == "making" || $cwd:t == "custom" || $cwd:t == "documentation") then

if ($USER != spws) then
  echo "--> getting list of source files in $SPWSDIR/making  ..."
  ls $SPWSDIR/making/*                  >> aa_tempfile
endif

  echo "--> getting list of source files in $SPWSINSTALLDIR/scripts  ..."
  ls $SPWSINSTALLDIR/scripts/*          >> aa_tempfile

  echo "--> getting list of source files in $SPSINSTALLDIR/scripts  ..."
  ls $SPSINSTALLDIR/scripts/*           >> aa_tempfile

  echo "--> getting list of source files in $SPSDIR/etc  ..."
  ls $SPSDIR/etc/*                      >> aa_tempfile

endif
endif


##---------------- save list of system app-defaults files --------------------##
##---------------- save list of system app-defaults files --------------------##
##---------------- save list of system app-defaults files --------------------##


if ($2 == "") then
if ($cwd:t == "app-defaults") then

if ($USER != spws) then
  echo "--> getting list of source files in $SPWSDIR/app-defaults  ..."
  ls $SPWSDIR/app-defaults/*            >> aa_tempfile
endif

  echo "--> getting list of source files in $SPWSINSTALLDIR/app-defaults  ..."
  ls $SPWSINSTALLDIR/app-defaults/*     >> aa_tempfile

endif
endif


##--------------------- save list of system ~sps files ----------------------##
##--------------------- save list of system ~sps files ----------------------##
##--------------------- save list of system ~sps files ----------------------##


if ($2 == "") then

  echo "--> getting list of source files in $SPSDIR/templates  ..."
  ls $SPSDIR/templates/*             >> aa_tempfile

  echo "--> getting list of source files in $SPSDIR/alpha  ..."
  ls $SPSDIR/alpha/include/*.h        >> aa_tempfile
  ls $SPSDIR/alpha/include/*.hh       >> aa_tempfile
  ls $SPSDIR/alpha/primitives/*/*.c   >> aa_tempfile
  ls $SPSDIR/alpha/primitives/*/*.cc  >> aa_tempfile
  ls $SPSDIR/alpha/primitives/*/*.f90 >> aa_tempfile
  ls $SPSDIR/alpha/processes/*/*.c    >> aa_tempfile
  ls $SPSDIR/alpha/processes/*/*.cc   >> aa_tempfile
  ls $SPSDIR/alpha/processes/*/*.f90  >> aa_tempfile

  echo "--> getting list of source files in $SPSDIR/beta  ..."
  ls $SPSDIR/beta/include/*.h        >> aa_tempfile
  ls $SPSDIR/beta/include/*.hh       >> aa_tempfile
  ls $SPSDIR/beta/primitives/*/*.c   >> aa_tempfile
  ls $SPSDIR/beta/primitives/*/*.cc  >> aa_tempfile
  ls $SPSDIR/beta/primitives/*/*.f90 >> aa_tempfile
  ls $SPSDIR/beta/processes/*/*.c    >> aa_tempfile
  ls $SPSDIR/beta/processes/*/*.cc   >> aa_tempfile
  ls $SPSDIR/beta/processes/*/*.f90  >> aa_tempfile
  ls $SPSDIR/beta/scripts/*          >> aa_tempfile

  echo "--> getting list of source files in $SPSDIR/production  ..."
  ls $SPSDIR/production/include/*.h        >> aa_tempfile
  ls $SPSDIR/production/include/*.hh       >> aa_tempfile
  ls $SPSDIR/production/primitives/*/*.c   >> aa_tempfile
  ls $SPSDIR/production/primitives/*/*.cc  >> aa_tempfile
  ls $SPSDIR/production/primitives/*/*.f90 >> aa_tempfile
  ls $SPSDIR/production/processes/*/*.c    >> aa_tempfile
  ls $SPSDIR/production/processes/*/*.cc   >> aa_tempfile
  ls $SPSDIR/production/processes/*/*.f90  >> aa_tempfile
  ls $SPSDIR/production/scripts/*          >> aa_tempfile

  echo "--> getting list of source files in $SPSDIR/*/programs  ..."
  ls $SPSDIR/beta/programs/*/*.c     >> aa_tempfile
  ls $SPSDIR/beta/programs/*/*.cc    >> aa_tempfile
  ls $SPSDIR/beta/programs/*/*.f90   >> aa_tempfile
  ls $SPSDIR/production/programs/*/*.c     >> aa_tempfile
  ls $SPSDIR/production/programs/*/*.cc    >> aa_tempfile
  ls $SPSDIR/production/programs/*/*.f90   >> aa_tempfile

endif


##-------------------- save list of system ~spws files ----------------------##
##-------------------- save list of system ~spws files ----------------------##
##-------------------- save list of system ~spws files ----------------------##


if ($2 == "") then
if ($cwd:t != "cps" && $cwd:t != "making" && $cwd:t != "app-defaults" \
        && $cwd:t != "archives" && $cwd:t != "documentation") then
if ($USER != spws) then

  echo "--> getting list of source files in $SPWSDIR  ..."
  ls $SPWSDIR/include/*.h               >> aa_tempfile
  ls $SPWSDIR/include/*.hh              >> aa_tempfile
  ls $SPWSDIR/include/*/*.h             >> aa_tempfile
  ls $SPWSDIR/include/*/*.hh            >> aa_tempfile
  ls $SPWSDIR/oop/*/*.cc                >> aa_tempfile
  ls $SPWSDIR/oop/*/*.c                 >> aa_tempfile
  ls $SPWSDIR/oop/*/*.f                 >> aa_tempfile
  ls $SPWSDIR/oop/*/*.f90               >> aa_tempfile
  ls $SPWSDIR/oop/*/*.h                 >> aa_tempfile
  ls $SPWSDIR/oop/*/*.hh                >> aa_tempfile
  ls $SPWSDIR/util/*/*.c                >> aa_tempfile
  ls $SPWSDIR/util/*/*.f                >> aa_tempfile
  ls $SPWSDIR/util/*/*.f90              >> aa_tempfile
  ls $SPWSDIR/util/*/*.h                >> aa_tempfile
  ls $SPWSDIR/programs/*/*.cc           >> aa_tempfile
  ls $SPWSDIR/programs/*/*.c            >> aa_tempfile
  ls $SPWSDIR/programs/*/*.f            >> aa_tempfile
  ls $SPWSDIR/programs/*/*.f90          >> aa_tempfile
  ls $SPWSDIR/programs/*/*.h            >> aa_tempfile
  ls $SPWSDIR/programs/*/*.hh           >> aa_tempfile

endif
endif
endif


##----------------- save flag marking start of local files -----------------##
##----------------- save flag marking start of local files -----------------##
##----------------- save flag marking start of local files -----------------##


echo starting_local_files          >> aa_tempfile


##------------------------ save list of local files -------------------------##
##------------------------ save list of local files -------------------------##
##------------------------ save list of local files -------------------------##


if ($cwd:t == "making") then

  echo "--> getting list of all files in local making directory  ..."
  \ls -F                           >> aa_tempfile

else if ($cwd:t == "custom") then

  echo "--> getting list of all files in local custom directory  ..."
  \ls -F                           >> aa_tempfile

else if ($cwd:t == "app-defaults") then

  echo "--> getting list of all files in local app-defaults directory  ..."
  \ls -F                           >> aa_tempfile

else if ($cwd:t == "documentation") then

  echo "--> getting list of all files in local documentation directory  ..."
  \ls -F                           >> aa_tempfile

else

  echo "--> getting list of source files in local personal directory  ..."
  ls *.h                           >> aa_tempfile
  ls *.hh                          >> aa_tempfile
  ls *.c                           >> aa_tempfile
  ls *.cc                          >> aa_tempfile
  ls *.f                           >> aa_tempfile
  ls *.f90                         >> aa_tempfile

endif


##------------------ remove various files from list -----------------------##
##------------------ remove various files from list -----------------------##
##------------------ remove various files from list -----------------------##

          # aa_tempfile and bb_tempfile are repeatedly swapped.

echo '--> removing files containing _tempfile  ...'
grep -v _tempfile < aa_tempfile > bb_tempfile

echo '--> removing files containing  test_     ...'
grep -v test_ < bb_tempfile > aa_tempfile

echo '--> removing files containing superwrap  ...'
grep -v superwrap < aa_tempfile > bb_tempfile

echo '--> removing files containing superproc  ...'
grep -v superproc < bb_tempfile > aa_tempfile

echo '--> removing files containing /obsolete  ...'
grep -v /obsolete < aa_tempfile > bb_tempfile

echo '--> removing files containing /save/     ...'
grep -v /save/ < bb_tempfile > aa_tempfile


##------------------- compare local and system files -----------------------##
##------------------- compare local and system files -----------------------##
##------------------- compare local and system files -----------------------##

 # need to use aa_tempfile or bb_tempfile, whichever was last modified above.

echo '--> doing comparisons between local files and system files  ...'

#$SPWSMAKEDIR/$ARCHITECTURE/sps_comparisons < aa_tempfile
~stoectr/making/$ARCHITECTURE/sps_comparisons < aa_tempfile


##----------------------- finish up and return ------------------------------##
##----------------------- finish up and return ------------------------------##
##----------------------- finish up and return ------------------------------##


\rm -f aa_tempfile bb_tempfile

echo '--> finished'


##------------------------------- end --------------------------------------##
##------------------------------- end --------------------------------------##
##------------------------------- end --------------------------------------##

