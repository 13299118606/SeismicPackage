#! /bin/csh -f

# check_rcs_files
#
# $Id: check_rcs_files,v 1.1 2004/05/25 22:03:20 spws Exp spws $
#
# Run this script to check the RCS files in the specified ~spws directory.
#
# Simply type this:
#
#               check_rcs_files  <directory>  <option>
#
# where  <directory> (required) is the desired RCS directory (full path).
# where  <option>    (optional) specifies which files should be listed.
#
#     option   files listed
#     ------   ------------
#       -1     not checked in or not checked out.
#       -2     not checked in, not checked out, or not matching.
#       -3     not checked in, not checked out, matching, or not matching.
#
# Option -2 is used for any other option (or unspecified option).
#
# This script verifies that all checked-out files in <directory> are the same
# as the latest versions of files in the RCS directory below <directory>.

#-------------------------------------------------------------------------------

set directory = $1
set option    = $2

if($directory == "") then
     echo RCS directory argument not set
     exit
endif

#-------------------------------------------------------------------------------

    cd $directory/..

    echo ""
    echo "directory $directory"
    echo ""

    foreach name ($directory/*,v)
        set name2 = `echo $name | tr ',' '.'`
        set name3 = $name2:r
        set name4 = $name3:t
        if (! -f $name4) then
            echo "   not checked out:                            $name4"
            continue
        endif
        if ($option == -1) then
            continue
        endif
        set rw = " r "
        if (-w $name4) then
            set rw = "r/w"
        endif
        set junkname = junktemp.$name4
        if (-e $junkname) then
            \rm $junkname
        endif
        \mv     $name4 $junkname
        co   -l $name4           >& /dev/null
        diff -h $name4 $junkname >& /dev/null
#       cmp  -s $name4 $junkname >& /dev/null
        if ($status != 0) then
            echo "   does not match latest version in RCS:  $rw  $name4"
        else if ($option == -3) then
            echo "   matching:                              $rw  $name4"
        endif
        \mv     $junkname $name4
    end

    foreach name ($directory/../*)
        if (! -f $name) then
            continue
        endif
        set name2 = $name:t
        set name3 = $directory/$name2,v
        if ($name2:r == makefile) then
            continue
        endif
        set rw = " r "
        if (-w $name2) then
            set rw = "r/w"
        endif
        if (! -f $name3) then
            echo "   not checked in:                        $rw  $name2"
        endif
    end

#-------------------------------------------------------------------------------

